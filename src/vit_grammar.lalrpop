use std::str::FromStr;
use crate::ast::{Expr, Opcode, Statement, Identifier};

grammar;

pub Program: Vec<Statement> = {
    InstructionBlock,
};

InstructionBlock: Vec<Statement> = {
    (Instruction)*
};

Instruction: Statement = {
    <Statement> ";",
    <Structure>
};

Structure: Statement = {
    "if" <p:Predicate> "{" <ib:(Instruction)*> "}" <t:IfTail?> => Statement::If(p, ib, t),
    "do" "{" <ib:(Instruction)*> "}" "until" <p:Predicate> ";" => Statement::Until(p, ib),
    "loop" "{" <ib:(Instruction)*> "}" => Statement::Loop(ib),
};

IfTail: Vec<Statement> = {
    "else" "{" <(Instruction)*> "}"
};

Statement: Statement = {
    "let" <id:ID> <e:Assign?> => Statement::Declaration(id, e),
    <id:ID> <e:Assign> => Statement::Assignment(id, e),
    Read,
    Write,
    "break" => Statement::Break,
};

Assign: Box<Expr> = {
    "=" <Expr>
};

Read: Statement = {
    "read" <ID> => Statement::Read(<>),
};

Write: Statement = {
    "write" <Literal> => Statement::WriteLiteral(<>),
    "write" <ID> => Statement::WriteId(<>),
};

pub Predicate: Box<Expr> = {
    Predicate Connective LogicalExpression => Box::new(Expr::Op(<>)),
    LogicalExpression,
};

Connective: Opcode = {
    "and" => Opcode::And,
    "or" => Opcode::Or,
};

LogicalExpression: Box<Expr> = {
    Expr LogicalOp Expr => Box::new(Expr::Op(<>)),
    "(" <Predicate> ")"
};

LogicalOp: Opcode = {
    "==" => Opcode::Eq,
    "!=" => Opcode::Neq,
    ">" => Opcode::Grt,
    "<" => Opcode::Let,
    ">=" => Opcode::Geq,
    "<=" => Opcode::Leq,
};

pub Expr: Box<Expr> = {
    Expr ExprOp Factor => Box::new(Expr::Op(<>)),
    Factor,
};

ExprOp: Opcode = {
    "+" => Opcode::Add,
    "-" => Opcode::Sub,
};

Factor: Box<Expr> = {
    Factor FactorOp Exp => Box::new(Expr::Op(<>)),
    Exp,
};

FactorOp: Opcode = {
    "*" => Opcode::Mul,
    "/" => Opcode::Div,
    "%" => Opcode::Mod,
};

Exp: Box<Expr> = {
    Term Expop Exp => Box::new(Expr::Op(<>)),
    Term,
};

Expop: Opcode = {
    "^" => Opcode::Exp
}

Term: Box<Expr> = {
    "(" <Expr> ")",
    <sign:"-"?> <n:Num> => Box::new(Expr::Number(sign.is_some(), n)),
    ID => Box::new(Expr::Id(<>)),
}

//Num: i32 = r"[0-9]+" => i32::from_str(<>).unwrap();
Num: Box<Expr> = <n:r"([0-9]+.)?[0-9]+"> => if n.contains('.') { Box::new(Expr::Float(f32::from_str(n).unwrap())) } else { Box::new(Expr::Integer(i32::from_str(<>).unwrap())) };
// Float: f32 = r"[0-9]+.[0-9]+" => Float(f32::from_str(<>).unwrap());
ID: Identifier = r"[a-zA-z][a-zA-z0-9_]*" => String::from(<>);
Literal: String = r"'.*'" => String::from(<>);